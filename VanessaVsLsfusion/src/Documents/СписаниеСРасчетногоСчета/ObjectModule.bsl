Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Договор.ДетализацияРасчетов = Перечисления.ТипыДетализацииРасчетов.ПоРасчетнымДокументам Тогда
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение
			И Расчеты.Итог("Сумма") > СуммаСНДС Тогда
			Сообщить("Ошибка при проведении " + Ссылка + ", сумма расчетов больше суммы документа");
			Отказ = Истина;
		КонецЕсли;
		
	Иначе
		Расчеты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	РасчетныйСчет = Константы.РасчетныйСчетПоУмолчанию.Получить();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.РасчетыСКонтрагентами.Записывать = Истина;
	
	Если Договор.ДетализацияРасчетов = Перечисления.ТипыДетализацииРасчетов.ПоРасчетнымДокументам Тогда
		
		ОставшаясяСумма = СуммаСНДС;
		
		Для А = 0 По Расчеты.Количество() - 1 Цикл
			
			Движение = Движения.РасчетыСКонтрагентами.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = Договор;
			Движение.РасчетныйДокумент = Расчеты[А].РасчетныйДокумент;
			Движение.Сумма = Расчеты[А].Сумма;
			
			ОставшаясяСумма = ОставшаясяСумма - Расчеты[А].Сумма;
			
		КонецЦикла;
		
		Если ОставшаясяСумма <> 0 Тогда
			Движение = Движения.РасчетыСКонтрагентами.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = Договор;
			Движение.РасчетныйДокумент = Ссылка;
			Движение.Сумма = ОставшаясяСумма;
		КонецЕсли;
		
	Иначе
		Движение = Движения.РасчетыСКонтрагентами.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Договор = Договор;
		Движение.Сумма = СуммаСНДС;
	КонецЕсли;
	
	Движения.ДенежныеСредства.Записывать = Истина;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ВложенныйЗапрос.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДенежныеСредстваОстатки.СуммаОстаток КАК СуммаОстаток
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредства.Остатки(&Период, ДенежнаяЯчейка = &ДенежнаяЯчейка) КАК ДенежныеСредстваОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДенежныеСредстваОбороты.СуммаОборот
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредства.Обороты(, &Период, Авто, ДенежнаяЯчейка = &ДенежнаяЯчейка) КАК ДенежныеСредстваОбороты
	|	ГДЕ
	|		ДенежныеСредстваОбороты.Регистратор = &Регистратор) КАК ВложенныйЗапрос";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", Сервер.ПолучитьГраницуИсключаяДокумент(Ссылка));
	Запрос.УстановитьПараметр("ДенежнаяЯчейка", РасчетныйСчет);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОстатокВДенежнойЯчейке = 0;
	Иначе
		
		ВыборкаИзЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаИзЗапроса.Следующий();
		
		ОстатокВДенежнойЯчейке = ВыборкаИзЗапроса.СуммаОстаток;
		
	КонецЕсли;
	
	Если СуммаСНДС > ОстатокВДенежнойЯчейке Тогда
		
		Сообщить("Ошибка при проведении " + Ссылка + ", отрицательный остаток денежных средств на расчетном счете: " + (СуммаСНДС - ОстатокВДенежнойЯчейке));
		
		Отказ = Истина;
		
	Иначе
		Движение = Движения.ДенежныеСредства.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.ДенежнаяЯчейка = РасчетныйСчет;
		Движение.Сумма = СуммаСНДС;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры
