&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	УстановитьВидимостьТабличнойЧастиРасчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаВключаетНДСПриИзменении(Элемент)
	
	ПредложитьПересчитатьСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	КлиентСервер.ПересчитатьШапкуДокументаПриИзмененииСуммы(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	КлиентСервер.ПересчитатьШапкуДокументаПриИзмененииСтавкиНДС(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура НДСПриИзменении(Элемент)
	
	КлиентСервер.ПересчитатьШапкуДокументаПриИзмененииНДС(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложитьПересчитатьСумму()
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ПредложитьПересчитатьСуммуЗавершение", ЭтотОбъект), "Пересчитать сумму?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложитьПересчитатьСуммуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПересчитатьСумму();
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСумму()
	
	КлиентСервер.ПересчитатьШапкуДокументаПриИзмененииСуммаВключаетНДС(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьТабличнойЧастиРасчеты();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьТабличнойЧастиРасчеты()
	
	Если Объект.Договор.ДетализацияРасчетов = Перечисления.ТипыДетализацииРасчетов.ПоРасчетнымДокументам Тогда
		Элементы.Расчеты.Видимость = Истина;
	Иначе
		Элементы.Расчеты.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасчеты(Команда)
	
	ЗаполнитьРасчетыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасчетыНаСервере()
	
	Объект.Расчеты.Очистить();
	
	ОставшаясяСумма = Объект.СуммаСНДС;
	
	Если ОставшаясяСумма <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетыСКонтрагентамиОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСКонтрагентамиОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.РасчетыСКонтрагентами.Остатки(
	|			&Период,
	|			Контрагент = &Контрагент
	|				И Договор = &Договор) КАК РасчетыСКонтрагентамиОстатки
	|ГДЕ
	|	РасчетыСКонтрагентамиОстатки.СуммаОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетыСКонтрагентамиОстатки.РасчетныйДокумент.Дата,
	|	РасчетыСКонтрагентамиОстатки.РасчетныйДокумент.Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", Сервер.ПолучитьГраницуИсключаяДокумент(Объект));
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Объект.Расчеты.Добавить();
		СтрокаТабличнойЧасти.РасчетныйДокумент = ВыборкаЗапроса.РасчетныйДокумент;
		
		Если ВыборкаЗапроса.Сумма > ОставшаясяСумма Тогда
			
			СтрокаТабличнойЧасти.Сумма = ВыборкаЗапроса.Сумма;
			
			ОставшаясяСумма = ОставшаясяСумма - ВыборкаЗапроса.Сумма;
			
		Иначе
			
			СтрокаТабличнойЧасти.Сумма = ОставшаясяСумма;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
