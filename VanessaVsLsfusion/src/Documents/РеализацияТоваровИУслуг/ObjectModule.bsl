Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Сумма = Номенклатура.Итог("Сумма");
	НДС = Номенклатура.Итог("НДС");
	СуммаСНДС = Номенклатура.Итог("СуммаСНДС");
	
	Если Договор.ДетализацияРасчетов = Перечисления.ТипыДетализацииРасчетов.ПоРасчетнымДокументам Тогда
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение
			И Расчеты.Итог("Сумма") > СуммаСНДС Тогда
			Сообщить("Ошибка при проведении " + Ссылка + ", сумма расчетов больше суммы документа");
			Отказ = Истина;
		КонецЕсли;
		
	Иначе
		Расчеты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	Склад = Константы.СкладПоУмолчанию.Получить();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ПродажиИСебестоимость.Записывать = Истина;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеализацияТоваровИУслугНоменклатура.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(РеализацияТоваровИУслугНоменклатура.НомерСтроки) КАК НомерСтроки,
	|	СУММА(РеализацияТоваровИУслугНоменклатура.Количество) КАК КоличествоПродажи,
	|	СУММА(РеализацияТоваровИУслугНоменклатура.СуммаСНДС) КАК СуммаПродажи
	|ПОМЕСТИТЬ ВТНоменклатура
	|ИЗ
	|	Документ.РеализацияТоваровИУслуг.Номенклатура КАК РеализацияТоваровИУслугНоменклатура
	|ГДЕ
	|	РеализацияТоваровИУслугНоменклатура.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровИУслугНоменклатура.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	НомерСтроки,
	|	КоличествоПродажи,
	|	СуммаПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(ВложенныйЗапрос.НомерСтроки) КАК НомерСтроки,
	|	ВложенныйЗапрос.Партия КАК Партия,
	|	ВложенныйЗапрос.Партия.Контрагент КАК Поставщик,
	|	ВложенныйЗапрос.Партия.Договор КАК ДоговорСПоставщиком,
	|	МАКСИМУМ(ВложенныйЗапрос.КоличествоПродажи) КАК КоличествоПродажи,
	|	СУММА(ВложенныйЗапрос.КоличествоОстаток) КАК КоличествоОстаток,
	|	МАКСИМУМ(ВложенныйЗапрос.СуммаПродажи) КАК СуммаПродажи,
	|	СУММА(ВложенныйЗапрос.СуммаОстаток) КАК СуммаОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТНоменклатура.Номенклатура КАК Номенклатура,
	|		ВТНоменклатура.НомерСтроки КАК НомерСтроки,
	|		ТоварыНаСкладах.Партия КАК Партия,
	|		ВТНоменклатура.КоличествоПродажи КАК КоличествоПродажи,
	|		ЕСТЬNULL(ТоварыНаСкладах.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|		ВТНоменклатура.СуммаПродажи КАК СуммаПродажи,
	|		ЕСТЬNULL(ТоварыНаСкладах.СуммаОстаток, 0) КАК СуммаОстаток
	|	ИЗ
	|		ВТНоменклатура КАК ВТНоменклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(, Склад = &Склад) КАК ТоварыНаСкладах
	|			ПО ВТНоменклатура.Номенклатура = ТоварыНаСкладах.Номенклатура
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТНоменклатура.Номенклатура,
	|		ВТНоменклатура.НомерСтроки,
	|		ТоварыНаСкладах.Партия,
	|		ВТНоменклатура.КоличествоПродажи,
	|		-ЕСТЬNULL(ТоварыНаСкладах.КоличествоОборот, 0),
	|		ВТНоменклатура.СуммаПродажи,
	|		-ЕСТЬNULL(ТоварыНаСкладах.СуммаОборот, 0)
	|	ИЗ
	|		ВТНоменклатура КАК ВТНоменклатура
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Обороты(, , Регистратор, Склад = &Склад) КАК ТоварыНаСкладах
	|			ПО ВТНоменклатура.Номенклатура = ТоварыНаСкладах.Номенклатура
	|				И (ТоварыНаСкладах.Регистратор = &Ссылка)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Партия,
	|	ВложенныйЗапрос.Партия.Контрагент,
	|	ВложенныйЗапрос.Партия.Договор
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(КоличествоПродажи),
	|	СУММА(КоличествоОстаток),
	|	МАКСИМУМ(СуммаПродажи),
	|	СУММА(СуммаОстаток)
	|ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНоменклатура";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзЗапросаПоТоварам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Номенклатура");
	
	Пока ВыборкаИзЗапросаПоТоварам.Следующий() Цикл
		
		Если ВыборкаИзЗапросаПоТоварам.Номенклатура.Услуга Тогда
			Движение = Движения.ПродажиИСебестоимость.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = ВыборкаИзЗапросаПоТоварам.Номенклатура;
			Движение.Покупатель = Контрагент;
			Движение.ДоговорСПокупателем = Договор;
			Движение.СуммаПродажи = ВыборкаИзЗапросаПоТоварам.СуммаПродажи;
		ИначеЕсли ВыборкаИзЗапросаПоТоварам.КоличествоПродажи > ВыборкаИзЗапросаПоТоварам.КоличествоОстаток Тогда
			
			Сообщить("Ошибка при проведении " + Ссылка + ", отрицательный остаток товаров на складе:");
			Сообщить("	Товар " + ВыборкаИзЗапросаПоТоварам.Номенклатура + ": " + (ВыборкаИзЗапросаПоТоварам.КоличествоОстаток - ВыборкаИзЗапросаПоТоварам.КоличествоПродажи));
			
			Пока ВыборкаИзЗапросаПоТоварам.Следующий() Цикл
				
				Если ВыборкаИзЗапросаПоТоварам.Количество > ВыборкаИзЗапросаПоТоварам.КоличествоОстаток Тогда
					Сообщить("	Товар " + ВыборкаИзЗапросаПоТоварам.Номенклатура + ": " + (ВыборкаИзЗапросаПоТоварам.КоличествоОстаток - ВыборкаИзЗапросаПоТоварам.КоличествоПродажи));
				КонецЕсли;
				
			КонецЦикла;
			
			Отказ = Истина;
			
			Прервать;
			
		Иначе
			
			ОсталосьСписатьКоличество = ВыборкаИзЗапросаПоТоварам.КоличествоПродажи;
			ОсталосьСписатьСуммаПродажи = ВыборкаИзЗапросаПоТоварам.СуммаПродажи;
			
			ВыборкаИзЗапроса = ВыборкаИзЗапросаПоТоварам.Выбрать();
			
			Пока ВыборкаИзЗапроса.Следующий()
				И ОсталосьСписатьКоличество <> 0 Цикл
				
				Если ОсталосьСписатьКоличество > ВыборкаИзЗапроса.КоличествоОстаток Тогда
					
					ДвижениеКоличество = ВыборкаИзЗапроса.КоличествоОстаток;
					ДвижениеСуммаСебестоимости = ВыборкаИзЗапроса.СуммаОстаток;
					
					Если ВыборкаИзЗапроса.КоличествоПродажи = 0 Тогда
						ДвижениеСуммаПродажи = 0;
					Иначе
						ДвижениеСуммаПродажи = ОсталосьСписатьСуммаПродажи / ОсталосьСписатьКоличество * ВыборкаИзЗапроса.КоличествоОстаток;
					КонецЕсли;
					
					ОсталосьСписатьКоличество = ОсталосьСписатьКоличество - ДвижениеКоличество;
					ОсталосьСписатьСуммаПродажи = ОсталосьСписатьСуммаПродажи - ДвижениеСуммаПродажи;
					
				Иначе
					
					ДвижениеКоличество = ОсталосьСписатьКоличество;
					
					Если ВыборкаИзЗапроса.КоличествоОстаток = 0 Тогда
						ДвижениеСуммаСебестоимости = 0;
					Иначе
						ДвижениеСуммаСебестоимости = ВыборкаИзЗапроса.СуммаОстаток / ВыборкаИзЗапроса.КоличествоОстаток * ОсталосьСписатьКоличество;
					КонецЕсли;
					
					ДвижениеСуммаПродажи = ОсталосьСписатьСуммаПродажи;
					
					ОсталосьСписатьКоличество = 0;
					ОсталосьСписатьСуммаПродажи = 0;
					
				КонецЕсли;
				
				Движение = Движения.ТоварыНаСкладах.ДобавитьРасход();
				Движение.Период = Дата;
				Движение.Склад = Склад;
				Движение.Номенклатура = ВыборкаИзЗапроса.Номенклатура;
				Движение.Партия = ВыборкаИзЗапроса.Партия;
				Движение.Количество = ДвижениеКоличество;
				Движение.Сумма = ДвижениеСуммаСебестоимости;
				
				Движение = Движения.ПродажиИСебестоимость.Добавить();
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаИзЗапроса.Номенклатура;
				Движение.Покупатель = Контрагент;
				Движение.ДоговорСПокупателем = Договор;
				Движение.Поставщик = ВыборкаИзЗапроса.Поставщик;
				Движение.ДоговорСПоставщиком = ВыборкаИзЗапроса.ДоговорСПоставщиком;
				Движение.Партия = ВыборкаИзЗапроса.Партия;
				Движение.СуммаПродажи = ДвижениеСуммаПродажи;
				Движение.СуммаСебестоимости = ДвижениеСуммаСебестоимости;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.РасчетыСКонтрагентами.Записывать = Истина;
	
	Если Договор.ДетализацияРасчетов = Перечисления.ТипыДетализацииРасчетов.ПоРасчетнымДокументам Тогда
		
		ОставшаясяСумма = СуммаСНДС;
		
		Для А = 0 По Расчеты.Количество() - 1 Цикл
			
			Движение = Движения.РасчетыСКонтрагентами.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = Договор;
			Движение.РасчетныйДокумент = Расчеты[А].РасчетныйДокумент;
			Движение.Сумма = Расчеты[А].Сумма;
			
			ОставшаясяСумма = ОставшаясяСумма - Расчеты[А].Сумма;
			
		КонецЦикла;
		
		Если ОставшаясяСумма <> 0 Тогда
			Движение = Движения.РасчетыСКонтрагентами.ДобавитьРасход();
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = Договор;
			Движение.РасчетныйДокумент = Ссылка;
			Движение.Сумма = ОставшаясяСумма;
		КонецЕсли;
		
	Иначе
		Движение = Движения.РасчетыСКонтрагентами.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Договор = Договор;
		Движение.Сумма = СуммаСНДС;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
КонецПроцедуры
