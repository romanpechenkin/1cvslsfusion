Процедура УстановкаПараметровСеанса(ТребуемыеПараметры)
	
	Если ТребуемыеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТребуемыеПараметры.Найти("ТекущийПользователь") <> Неопределено Тогда
		УстановитьПараметрСеансаТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрСеансаТекущийПользователь()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Пользователи.Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.УникальныйИдентификатор = &УникальныйИдентификатор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("УникальныйИдентификатор", ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
		НовыйПользователь.Наименование = ИмяПользователя();
		НовыйПользователь.УникальныйИдентификатор = ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор;
		НовыйПользователь.Записать();
		
		ПараметрыСеанса.ТекущийПользователь = НовыйПользователь.Ссылка;
		
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПараметрыСеанса.ТекущийПользователь = Выборка.Ссылка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры
