&НаКлиенте
Процедура ВыбратьКартинку(Команда)
	
	НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("ВыбратьКартинкуЗавершение", ЭтотОбъект), Новый ОписаниеОповещения("ВыбратьКартинкуХодВыполнения", ЭтотОбъект), Новый ОписаниеОповещения("ВыбратьКартинкуПередНачалом", ЭтотОбъект), Картинка, Новый ПараметрыДиалогаПомещенияФайлов(, Ложь, "Картинка (*.bmp;*.gif;*.ico;*.jpeg;*.jpg;*.png;*.tif;*.tiff;*.svg)|*.bmp;*.gif;*.ico;*.jpeg;*.jpg;*.png;*.tif;*.tiff;*.svg"), УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКартинку(Команда)
	
	Картинка = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКартинкуЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	
	Элементы.ПрогрессЗагрузкиКартинки.Видимость = Ложь;
	Элементы.ОтменитьЗагрузкуКартинки.Видимость = Ложь;
	
	Если ОписаниеПомещенногоФайла = Неопределено
		ИЛИ ОписаниеПомещенногоФайла.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	Картинка = ОписаниеПомещенногоФайла.Адрес;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКартинкуХодВыполнения(ПомещаемыйФайл, Помещено, ОтказОтПомещенияФайла, ДополнительныеПараметры) Экспорт
	
	Если ЗагрузкаКартинкиОтменена Тогда
		
		ОтказОтПомещенияФайла = Истина;
		
		Элементы.ПрогрессЗагрузкиКартинки.Видимость = Ложь;
		Элементы.ОтменитьЗагрузкуКартинки.Видимость = Ложь;
		
		ЗагрузкаКартинкиОтменена = Ложь;
		ПрогрессЗагрузкиКартинки = 0;
		
	Иначе
		ПрогрессЗагрузкиКартинки = Помещено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКартинкуПередНачалом(ПомещаемыйФайл, ОтказОтПомещенияФайла, ДополнительныеПараметры) Экспорт
	
	Если ПомещаемыйФайл.Размер() > 5 * 1024 * 1024 Тогда
		ПоказатьПредупреждение(, "Размер файла не должен превышать 5MB");
		ОтказОтПомещенияФайла = Истина;
	КонецЕсли;
	
	ЗагрузкаКартинкиОтменена = Ложь;
	ПрогрессЗагрузкиКартинки = 0;
	
	Элементы.ПрогрессЗагрузкиКартинки.Видимость = Истина;
	Элементы.ОтменитьЗагрузкуКартинки.Видимость = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Картинка = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Данные");
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Картинка) Тогда
		ТекущийОбъект.Данные = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Картинка));
	Иначе
		ТекущийОбъект.Данные = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗагрузкуКартинки(Команда)
	
	Элементы.ПрогрессЗагрузкиКартинки.Видимость = Ложь;
	Элементы.ОтменитьЗагрузкуКартинки.Видимость = Ложь;
	
	ЗагрузкаКартинкиОтменена = Истина;
	
КонецПроцедуры
