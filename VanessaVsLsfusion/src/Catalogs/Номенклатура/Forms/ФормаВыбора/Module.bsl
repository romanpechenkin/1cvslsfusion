&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьИспользованиеДереваГрупп();
	
	Поля = Новый Массив;
	Поля.Добавить("ОстатокНаЛету");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(Поля);
	Список.УстановитьОграниченияИспользованияВПорядке(Поля);
	Список.УстановитьОграниченияИспользованияВОтборе(Поля);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	НастроитьИспользованиеДереваГрупп();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользованиеДереваГруппПриИзменении(Элемент)
	
	НастроитьИспользованиеДереваГрупп();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриАктивизацииСтроки(Элемент)
	
	ДеревоПриАктивизацииСтрокиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДеревоПриАктивизацииСтрокиНаСервере()
	
	Если Не ЗначениеЗаполнено(Элементы.Дерево.ТекущаяСтрока) Тогда
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Ложь;
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Ложь;
	ИначеЕсли ИспользованиеДереваГрупп = 1 Тогда
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Истина;
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Ложь;
	Иначе
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Ложь;
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Истина;
	КонецЕсли;
	
	Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[2].Использование = Истина;
	
	Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].ПравоеЗначение = Элементы.Дерево.ТекущаяСтрока;
	Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].ПравоеЗначение = Элементы.Дерево.ТекущаяСтрока;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьИспользованиеДереваГрупп()
	
	Если ИспользованиеДереваГрупп = 0 Тогда
		
		Элементы.Дерево.Видимость = Ложь;
		Элементы.ДеревоКомпоновщикНастроекПользовательскиеНастройки.Видимость = Ложь;
		
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Ложь;
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Ложь;
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[2].Использование = Ложь;
		
		Элементы.Список.Отображение = ОтображениеТаблицы.ИерархическийСписок;
		
	Иначе
		
		Элементы.Дерево.Видимость = Истина;
		Элементы.ДеревоКомпоновщикНастроекПользовательскиеНастройки.Видимость = Истина;
		
		Если Не ЗначениеЗаполнено(Элементы.Дерево.ТекущаяСтрока) Тогда
			Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Ложь;
			Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Ложь;
		ИначеЕсли ИспользованиеДереваГрупп = 1 Тогда
			Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Истина;
			Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Ложь;
		Иначе
			Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[0].Использование = Ложь;
			Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[1].Использование = Истина;
		КонецЕсли;
		
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы[2].Использование = Истина;
		
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток КАК Остаток
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(, Номенклатура В (&Номенклатура)) КАК ТоварыНаСкладахОстатки";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Номенклатура", Строки.ПолучитьКлючи());
	Результат = Запрос.Выполнить();
	
	Остатки = Новый Соответствие;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Остатки.Вставить(Выборка.Номенклатура, Выборка.Остаток);
	КонецЦикла;
	
	Для Каждого Строка Из Строки Цикл
		Строка.Значение.Данные.ОстатокНаЛету = Остатки.Получить(Строка.Ключ);
	КонецЦикла;
	
КонецПроцедуры
